/*
 * Copyright 2010-2010 LinkedIn, Inc
 * Portions Copyright (c) 2013-2014 Yan Pujante
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations under
 * the License.
 */

buildscript {
  repositories {
    if(project.hasProperty('maven.buildscript.repository'))
    {
      maven {
        url new File(project.properties["maven.buildscript.repository"]).toURI()
      }
    }

    if(project.hasProperty('ivy.buildscript.repository'))
    {
      ivy {
        url = new File(project.properties['ivy.buildscript.repository'])
        layout 'maven'
      }
    }

   // if you want to disallow jcenter
   if(project.properties['no.jcenter'] != 'true')
   {
     maven {
       url 'http://jcenter.bintray.com'
     }
   }

   // if you want to disallow maven central
   if(project.properties['no.maven.central'] != 'true')
   {
     mavenCentral()
   }
 }

  dependencies {
    classpath 'org.pongasoft:org.linkedin.gradle-plugins:2.2.6'
  }
}

apply plugin: 'org.linkedin.userConfig'
apply plugin: 'org.linkedin.spec'

ext {
  topBuildDir = userConfig.top.build.dir ?: "${rootDir}/out/build"
}

allprojects {
  apply plugin: 'idea'
  group = spec.group
  version = spec.version
}

// we use git
idea.project {
  jdkName = spec.versions.jdk
  languageLevel = spec.versions.jdk
  ipr.withXml { root ->
    root.asNode().component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
  }
}

subprojects {
  apply plugin: 'org.linkedin.repository'

  apply plugin: 'java'
  apply plugin: 'maven'

  allRepositories.build.configure()

  sourceCompatibility = spec.versions.jdk
  targetCompatibility = spec.versions.jdk

  buildDir = "${topBuildDir}${project.path.replace(':','/')}".toString()
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.11'
}
